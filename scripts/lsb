#!/bin/env nu

# Lookup passages and search phrases from the LSB Bible
def main [
  ...query: string # Bible query
] {
  let result = query-lsb-api $query
  try {
    $result
    | if ($in | is-standard-passage) {
      format-passage
    } else {
      format-search-results
    }
  } catch {
    print $"ERROR: ($in)"
    print ($result | is-standard-passage)
    print $result
  }
}

def query-lsb-api [query] {
  {
    q: ($query | str join " ")
  }
  | url build-query
  | $"https://read.lsbible.org/_next/data/n6yZnAeWmTcbY7mfSVTSp/index.json?($in)"
  | http get $in
  | get pageProps
}

def is-standard-passage [] {
  $in.passages? != null
}

def format-passage [] {
  let stdin = $in
  plugin use query
  $stdin
  | get passages
  | get passageHtml
  | first
  | query web --query 'span .prose'
  | flatten
  | where $it != '*'
  | str join
}

def format-search-results [] {
  get initialItems
  | rename reference content
  | update reference {
    parse "{book}-{chapter}-{verse}"
    | first
    | update book { into-book }
    | update chapter { str trim --left --char "0" }
    | update verse { str trim --left --char "0" }
    | $"($in.book) ($in.chapter):($in.verse)"
  }
  | update content { str replace -a '<b>' (ansi red) }
  | update content { str replace -a '</b>' (ansi reset) }
}

def into-book [] {
  match $in {
    '01' => 'Genesis',
    '02' => 'Exodus',
    '03' => 'Leviticus',
    '04' => 'Numbers',
    '05' => 'Deuteronomy',
    '06' => 'Joshua',
    '07' => 'Judges',
    '08' => 'Ruth',
    '09' => '1 Samuel',
    '10' => '2 Samuel',
    '11' => '1 Kings',
    '12' => '2 Kings',
    '13' => '1 Chronicles',
    '14' => '2 Chronicles',
    '15' => 'Ezra',
    '16' => 'Nehemiah',
    '17' => 'Esther',
    '18' => 'Job',
    '19' => 'Psalms',
    '20' => 'Proverbs',
    '21' => 'Ecclesiastes',
    '22' => 'Song of Solomon',
    '23' => 'Isaiah',
    '24' => 'Jeremiah',
    '25' => 'Lamentations',
    '26' => 'Ezekiel',
    '27' => 'Daniel',
    '28' => 'Hosea',
    '29' => 'Joel',
    '30' => 'Amos',
    '31' => 'Obadiah',
    '32' => 'Jonah',
    '33' => 'Micah',
    '34' => 'Nahum',
    '35' => 'Habakkuk',
    '36' => 'Zephaniah',
    '37' => 'Haggai',
    '38' => 'Zechariah',
    '39' => 'Malachi',
    '40' => 'Matthew',
    '41' => 'Mark',
    '42' => 'Luke',
    '43' => 'John',
    '44' => 'Acts',
    '45' => 'Romans',
    '46' => '1 Corinthians',
    '47' => '2 Corinthians',
    '48' => 'Galatians',
    '49' => 'Ephesians',
    '50' => 'Philippians',
    '51' => 'Colossians',
    '52' => '1 Thessalonians',
    '53' => '2 Thessalonians',
    '54' => '1 Timothy',
    '55' => '2 Timothy',
    '56' => 'Titus',
    '57' => 'Philemon',
    '58' => 'Hebrews',
    '59' => 'James',
    '60' => '1 Peter',
    '61' => '2 Peter',
    '62' => '1 John',
    '63' => '2 John',
    '64' => '3 John',
    '65' => 'Jude',
    '66' => 'Revelation',
    _ => $in
  }
}
