#!/bin/env nu

use std log

def --wrapped main [
  verb
  --raw
  --json
  ...rest
] {
  if (which dotnet | is-empty) {
    log error "`dotnet` not on path"
    return
  }
  
  match $verb {
    "build" => { build $raw $json $rest }
    "test" => { test $raw $json $rest }
    _ => { dotnet $verb ($rest | str join ' ') }
  }
}

def build [
  raw
  json
  rest
] {
  let input = dotnet build ($rest | str join ' ')
  if ($raw) {
    return $input
  }

  parse-into-structure $input
  | if ($json) {
    to json
  } else {
    each {|project| project-to-string $project}
    | flatten
    | str join (char newline)
  }
}

def test [
  raw
  json
  rest
] {
  let input = dotnet test ($rest | str join ' ')
  if ($raw) {
    return $input
  }

  let structure = parse-into-structure $input
  let tests = parse-into-tests $input

  if ($json) {
    $structure
    | append $tests
    | to json
  } else {
    $structure
    | each {|project| project-to-string $project}
    | append (
      $tests
      | each {|tests| tests-to-string $tests}
    )
    | flatten
    | str join (char newline)
  }
}

def parse-into-structure [output] {
  let results =  $output | lines | uniq
  let base_directory = find-base-directory (pwd)
  let projects = $results
  | parse "  {name} -> {dll}"
  | each {|project|
    {
      project: $project.name
    }
  }
  
  $results
  | parse "{path}({line},{column}): {level} {code}: {message} [{project}]"
  | update path {|it|
    $it.path
    | if ($in | str starts-with $base_directory) {
      path relative-to $base_directory
    } else {
      $in
    }
  }
  | update message {|it| $it.message | str trim }
  | update project {|it| $it.project | path basename | parse "{name}.{language}proj" | get 0.name }
  | group-by project
  | transpose project messages
  | join --outer $projects project
}

def parse-into-tests [output] {
  $output
  | lines
  | parse "{beginning}Failed:{fails}, Passed:{passes}, Skipped:{skips}, Total:{total}, Duration:{duration} - {dll} ({runtime})"
  | update fails {into int}
  | update passes {into int}
  | update skips {into int}
  | update total {into int}
  | update duration {|it|
    $it.duration
    | str replace ' ms' 'ms'
    | str replace ' s' 'sec'
    | into duration
  }
  | reject beginning
}

def project-to-string [project] {
  $project.messages?
  | group-by level
  | transpose level messages
  | each {|level| level-to-string $level}
  | prepend $"(colorize ($in | length))($project.project)(ansi reset)"
  | flatten
}

def tests-to-string [tests] {
  $tests
  | each {|test|
    ""
    | append $"Passed:  ($test.passes)"
    | append $"Failed:  ($test.fails)"
    | append $"Skipped: ($test.fails)"
    | append $"-------------"
    | append $"Total:   ($test.total)"
    | str join (char newline)
  }
}

def level-to-string [level] {
  let count = $level.messages | length
  $level.messages
  | group-by message
  | transpose code messages
  | each {|code| code-to-string $code}
  | prepend $"  (colorize $level.level)($count) ($level.level)(ansi reset)"
  | flatten
}

def code-to-string [code] {
  let count = $code.messages | length
  $code.messages
  | each {|message| message-to-string $message}
  | prepend $"    ($count) ($code.code)"
  | flatten
}

def message-to-string [message] {
  $"      ($message.path)\(($message.line),($message.column))"
}

def find-base-directory [directory] {
  generate $directory {|dir|
    let parent = up-a-directory $dir
    if $dir == $parent {
      { out: $dir }
    } else {
      { out: $parent, next: $parent }
    }
  }
  | prepend $directory
  | skip while {|dir| not (is-base-directory $dir)}
  | get -i 0
  | default "/"
}

def up-a-directory [directory] {
  $directory | append ".." | path join | path expand
}

def is-base-directory [directory] {
  $directory | append .git | path join | path exists
}

def colorize [input] {
  match $input {
    "warning" => (ansi yellow)
    "error" => (ansi red)
    0 => (ansi green)
    _ => (ansi reset)
  }
}
