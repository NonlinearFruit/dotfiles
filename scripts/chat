#!/usr/bin/env nu

# Send and read chats
def main [
  ...msg: string  # Message to send
  --user: string  # User for message
  --read          # Read most recent
  --age: duration = 2hr
] {
  if $read {
    read-most-recent-message $user $age
  } else {
    post-message $msg $user
  }
}

def read-most-recent-message [user age] {
  read-file
  | where $user == null or $it.user == $user
  | where (date now) - $it.time < $age
  | if ($in | is-empty) {
    ""
  } else {
    last
    | if $in.user != null {
      $"($in.message) -($in.user)"
    } else {
      $in.message
    }
  }
}

def post-message [msg user] {
  if $msg == null or ($msg | is-empty) {
    error "No message provided"
  }

  read-file
  | append {
    message: ($msg | str join (char space))
    user: $user
    time: (date now)
  }
  | save-file
}

def read-file [] {
  let file = (filepath)
  touch $file
  open $file
  | from yaml
  | if $in == null {
    []
  } else {
    update time { into datetime }
    | sort-by time
  }
}

def save-file [] {
  to yaml
  | save -f (filepath)
}

def filepath [] {
  "~/.chat-history"
  | path expand
}

def error [msg] {
  print $"(ansi red)($msg)(ansi reset)"
  exit 1
}
