#!/usr/bin/env nu

def main [
  message = ""        # Message to post
  --list-messages(-l) # Print the messages
  --limit = 0         # Limit how many messages to show
  --follow(-f)        # Continuously print messages
  --server(-s) = "~/.chat-history" # File used for persistence
] {
  let session = ^tmux display-message -p "#{session_name}"
  mut server = $server | path expand
  if $message != "" {
    post-message $server $session $message
  }
  if $list_messages and $follow {
    follow-messages $server $limit
  } else if $list_messages {
    list-messages $server $limit
  }
}

def post-message [server session message] {
  {
    date: (date now)
    session: $session
    message: $message
  }
  | to json
  | save -a $server
}

def follow-messages [server limit] {
  loop {
    let messages = list-messages $server $limit
    clear
    print $messages
    sleep 300ms
  }
}

def list-messages [server limit] {
  open $server
  | jq --slurp
  | from json
  | if $limit < 1 {
    $in
  } else {
    last $limit
  }
  | each { format-message $in }
  | to text
}

def format-message [message] {
  $"[($message.date | date humanize | fill --width 14)] ($message.session): ($message.message)"
}
