#!/usr/bin/env nu
# Random Word Generator
# Inspired by:
#      - https://linuxconfig.org/random-word-generator
#      - http://stackoverflow.com/a/14203146/4769802
#      - http://www.unix.com/shell-programming-and-scripting/156551-check-whether-string-begin-uppercase-lowercase-digit.html

# Outputs random (cryptographically secure) words from ~/scripts/words.txt
def main [
    --with-entropy            # Include the entropy of the generated words
    --as-json                 # Print as json
    number_of_words?: int = 1 # Number of words to generate
  ] {
  let max_int = 16777216
  let all_non_random_words = "~/scripts/words.txt" | path expand | open $in | lines
  let total_options = ($all_non_random_words | length) # apprx # of options

  let words = if $number_of_words > 0 {
      1..$number_of_words
      | each {|x|
        od -N3 -An -i /dev/urandom
        | into int
        | $in * $total_options / $max_int
        | math round
      }
      | each {|index|
        $all_non_random_words
        | get $index
      }
    } else {
      []
    }

  if $with_entropy {
    let entropy = $total_options | math log 2 | $in * $number_of_words | math round --precision 3
    if $as_json {
      {
        entropy: $entropy
        words: $words
      }
      | to json
    } else {
      echo $"($number_of_words) word\(s) out of ($total_options) produces ($entropy) bits of entropy."
      $words | str join (char newline)
    }
  } else {
    if $as_json {
      {
        words: $words
      }
      | to json
    } else {
      $words | str join (char newline)
    }
  }
}
