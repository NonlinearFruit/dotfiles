#!/usr/bin/env nu
# Random Word Generator
# Inspired by:
#      - https://linuxconfig.org/random-word-generator
#      - http://stackoverflow.com/a/14203146/4769802
#      - http://www.unix.com/shell-programming-and-scripting/156551-check-whether-string-begin-uppercase-lowercase-digit.html

def main [
    --with-entropy        # Include the entropy of the generated words
    --as-json              # Print as json
    number_of_words?: int  # Number of words to generate (default: 1)
  ] {
  let count = $number_of_words | default 1
  let max_int = 16777216
  let all_non_random_words = open "~/scripts/words.txt" | lines
  let total_options = ($all_non_random_words | length) # apprx # of options

  let words = 1..$count
    | each {|x|
      od -N3 -An -i /dev/urandom 
      | into int 
      | $in * $total_options / $max_int 
      | math round
    }
    | each {|index|
      $all_non_random_words
      | get $index
    }

  if $with_entropy {
    let entropy = $total_options | math log 2 | $in * $count | math round --precision 3
    if $as_json {
      {
        entropy: $entropy
        words: $words
      }
      | to json
    } else {
      echo $"($count) word\(s) out of ($total_options) produces ($entropy) bits of entropy."
      $words | str join (char newline)
    }
  } else {
    if $as_json {
      $words | to json
    } else {
      $words | str join (char newline)
    }
  }
}
