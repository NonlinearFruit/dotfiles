#!/bin/env nu

# Lookup passage from the ESV Bible
def main [
  --include-passage-references # Show passage references
  --include-verse-numbers      # Show verse numbers
  --include-footnotes          # Show the footnotes
  --include-headings           # Show the passage headings
  ...passage: string           # Bible passage to lookup
] {
  let headers = ["Authorization" "Token 7bf823cec4289f8fbce0b802be2cdeb22fd79c5b"]

  {
    include-passage-references: ($include_passage_references | into string)
    include-verse-numbers: ($include_verse_numbers | into string)
    include-footnotes: ($include_footnotes | into string)
    include-headings: ($include_headings | into string)
    q: ($passage | str join " ")
  }
  | url build-query
  | $"https://api.esv.org/v3/passage/text/?($in)"
  | http get $in --headers $headers
  | get passages
  | first
  | str replace --all '“' '"'
  | str replace --all '”' '"'
  | str replace --all "’" "'"
}
