#!/bin/env nu

# Summon all tmux clients to your window
def main [
  session_filter = ''
  --prompt(-p)
] {
  let window_id = get-my-window-id
  get-clients-with-session
  | if $prompt {
    input list --multi 'Select avengers' --display session
  } else {
    where ($it.session | str contains $session_filter)
  }
  | do {|it| print "Avengers assemble!"; $it} $in
  | each {|client|
    print $"  ($client.session)"
    move-to-window $client $window_id
    $client
  }
  | each {|client|
    update-status-line $client.id
  }
  null
}

def get-my-window-id [] {
  tmux display-message -p '#{window_id}'
  | str trim
}

def get-clients-with-session [] {
  tmux list-clients
  | lines
  | parse "{id}: {session} {rest}"
}

def move-to-window [client window] {
  tmux switch-client -c $client.id -t $"($client.session):($window)"
}

def update-status-line [client] {
  tmux refresh-client -t $client -S
}
